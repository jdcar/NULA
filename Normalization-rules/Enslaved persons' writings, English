rule "Add LCSH to local tags"
priority 4
when 
(exists "650.a.Slaves' writings, English*")
then 
addSubField "650.2.lcsh" if (exists "650.{*,0}.a.Slaves' writings, English*")
copyField "650" to "947" if (exists "650.a.Slaves' writings, English*")
removeSubField "650.2" if (exists "650.{*,0}")

end

rule "Flip from LCSH to NULA"
priority 3
when
(true)
then
replaceContents "650.a.Slaves' writings, English\\\\." with "Enslaved persons' writings, English." if(exists "650.{*,0}.a.Slaves' writings, English\\\\.")
replaceContents "650.a.Slaves' writings, English" with "Enslaved persons' writings, English" if(exists "650.{*,0}.a.Slaves' writings, English")
changeSecondIndicator "650" to "7" if (exists "650.a.Enslaved persons' writings, English\\\\.")
changeSecondIndicator "650" to "7" if (exists "650.a.Enslaved persons' writings, English")

addSubField "650.2.local"  if (exists "650.{*,7}.a.Enslaved persons' writings, English\\\\.")
addSubField "650.2.local"  if (exists "650.{*,7}.a.Enslaved persons' writings, English")

end

rule "Delete FAST headings"
priority 2
when 
(true)
then
removeField "650" if (exists "650.{*,7}.a.Slaves' writings, English*")
end

rule "Add note"
priority 1
when
(not exists "949.a.nula")
then
addField "949.a.nula" 
addSubField "949.e.jdc" if (exists "949.a.nula")
addSubField "949.c.20210805" if (exists "949.a.nula")
end
